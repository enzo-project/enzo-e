#------------------------------
# IMPORTS
#------------------------------

Import('env')
Import('type')
Import('use_papi')

#------------------------------
# DEPENDENCIES
#------------------------------

components = ['method',
	      'mesh', 'field',
	      'memory','parallel',
	      'parameters','monitor',
	      'performance','error',
	      'cello']

if (use_papi):
	components = components + ['papi','pfm']

libraries = ['png','$FORTRANLIBS']

#------------------------------
# SOURCE
#------------------------------

includes = [Glob('simulation*hpp')]
sources  = [Glob("simulation*cpp")]

#------------------------------
# ENVIRONMENTS
#------------------------------

env = env.Clone(LIBS=[components,libraries])

#------------------------------
# TARGETS
#------------------------------

libraries       = env.Library ('simulation', [sources])

if (type == "charm"):
   env.CharmBuilder('simulation.decl.h','simulation.ci',ARG='simulation')
   env.CharmBuilder('simulation.def.h', 'simulation.ci',ARG='simulation')
   includes = includes + ['simulation.decl.h','simulation.def.h'];

#------------------------------
# INSTALL
#------------------------------

env.Alias('install-inc',env.Install ('#/include',includes))
env.Alias('install-lib',env.Install ('#/lib/'+type,libraries))



