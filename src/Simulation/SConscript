#------------------------------
# IMPORTS
#------------------------------

Import('env')
Import('parallel_type')
Import('use_papi')

#------------------------------
# DEPENDENCIES
#------------------------------

components = ['error','parameters','memory','enzo','parallel','enzo-ppm','mesh', 'field','monitor','method','cello']

if (use_papi):
	components = components + ['papi','pfm']

libraries = ['png','$FORTRANLIBS']

#------------------------------
# ENVIRONMENTS
#------------------------------

env = env.Clone(LIBS=[components,libraries])

#------------------------------
# SOURCE
#------------------------------

includes = [Glob('simulation*hpp')]
sources  = [Glob("simulation*cpp")]

#------------------------------
# TARGETS
#------------------------------

libraries       = env.Library ('simulation', [sources])
test_simulation = env.Program (['test_Simulation.cpp',sources])
tests           = [test_simulation];

if (parallel_type == "charm"):
	test = 'test_Simulation'
	env.CharmBuilder(test + '.decl.h',test + '.ci',ARG = test)
	env.Depends(env.Object(test + '.cpp'),test + '.decl.h')

#------------------------------
# INSTALL
#------------------------------

env.Alias('install-bin',env.Install ('#/bin', tests))
env.Alias('install-inc',env.Install ('#/include',includes))
env.Alias('install-lib',env.Install ('#/lib',    libraries))



