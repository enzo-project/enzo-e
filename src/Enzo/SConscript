SConscript('ppm/SConscript')
SConscript('ppml/SConscript')

#------------------------------
# IMPORTS
#------------------------------

Import('env')
Import('parallel_type')
Import('use_papi')
Import('use_hdf5')

#------------------------------
# DEPENDENCIES
#------------------------------

components = ['disk','enzo-ppm', 'field', 'error','memory', 'simulation', 'parallel', 'mesh', 'parameters', 'monitor', 'method', 'cello']

if (use_papi):
	components = components + ['papi','pfm']
if (use_hdf5):
	components = components + ['hdf5']

libraries  = ['png', '$FORTRANLIBS']

#------------------------------
# ENVIRONMENTS
#------------------------------

env = env.Clone(LIBS=[components,libraries])

#------------------------------
# SOURCE
#------------------------------

includes = [Glob('enzo*hpp')]
sources  = [Glob('enzo_*cpp')]

#------------------------------
# TARGETS
#------------------------------

libraries     = env.Library ('enzo', sources)
test_enzo_p   = env.Program (['enzo-p.cpp', sources])

binaries = [test_enzo_p]

if (parallel_type == "charm"):

	test = 'enzo-p'
	testc = 'enzo_p' # required since class name cannot contain '-'
	env.CharmBuilder(testc + '.decl.h',test + '.ci',ARG = testc)
	env.Depends(env.Object(test + '.cpp'),testc + '.decl.h')

#------------------------------
# INSTALL
#------------------------------

env.Alias('install-inc',env.Install ('#/include',includes))
env.Alias('install-lib',env.Install ('#/lib',    libraries))
env.Alias('install-bin',env.Install ('#/bin',    binaries))



