Import('env')

components = ['error','parallel', 'monitor', 'png']
env = env.Clone(LIBS=[components])

includes = [Glob('parameters*hpp'),'parse.h']

env.Alias('install-inc',env.Install ('#/include',includes))

sources  = ['parse.tab.c', 'lex.yy.c', 'param.cpp', Glob('parameters*cpp') ]

env.CFile(target = ['parse.tab.c', 'parse.tab.h'], 
          source = 'parse.y',
          YACCFLAGS='-d -t')

env.CFile(target = 'lex.yy.c', source = 'parse.l')

libraries = env.Library ('parameters', [sources])

env.Alias('install-lib',env.Install ('#/lib',libraries))

test_parameters = env.Program (['test_Parameters.cpp', sources])
test_parse      = env.Program (['test_Parse.cpp', sources])
inputs          = Glob('*.in')
env.Alias('install-bin', env.Install ('$BINPATH', 
	[test_parameters, test_parse, inputs]))




