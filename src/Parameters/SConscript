#------------------------------
# IMPORTS
#------------------------------

Import('env')
Import('parallel_type')

#------------------------------
# DEPENDENCIES
#------------------------------

components = ['error','parallel', 'monitor', 'cello']
libraries  = ['png']

#------------------------------
# ENVIRONMENTS
#------------------------------

env = env.Clone(LIBS=[components,libraries])

#------------------------------
# SOURCE
#------------------------------

includes = [Glob('parameters*hpp'),'parse.h']
sources  = ['parse.tab.c', 'lex.yy.c', Glob('parameters*cpp') ]

#@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
# Uncomment when parse.l or parse.y change
#@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
# env.CFile(target = 'lex.yy.c', source = 'parse.l')
# env.CFile(target = ['parse.tab.c', 'parse.tab.h'], source = 'parse.y', YACCFLAGS='-d -t')
#@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

if (parallel_type == "charm"):
	test = 'test_Parameters'
	env.CharmBuilder(test + '.decl.h',test + '.ci',ARG = test)
	env.Depends(env.Object(test + '.cpp'),test + '.decl.h')
	test = 'test_Parse'
	env.CharmBuilder(test + '.decl.h',test + '.ci',ARG = test)
	env.Depends(env.Object(test + '.cpp'),test + '.decl.h')

#------------------------------
# TARGETS
#------------------------------

libraries = env.Library ('parameters', [sources])
test_parameters = env.Program (['test_Parameters.cpp', sources])
test_parse      = env.Program (['test_Parse.cpp', sources])

tests = [test_parameters, test_parse]

#------------------------------
# INSTALL
#------------------------------

env.Alias('install-inc',env.Install ('#/include',includes))
env.Alias('install-lib',env.Install ('#/lib',libraries))
env.Alias('install-bin',env.Install ('#/bin', tests))



