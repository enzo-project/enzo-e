#------------------------------
# IMPORTS
#------------------------------

Import('env')
Import('type')

Import('bin_path')
Import('inc_path')
Import('lib_path')

#------------------------------
# DEPENDENCIES
#------------------------------

components = ['parallel',
	      'error','monitor','performance', 
	      'test','cello']

#------------------------------
# SOURCE
#------------------------------

includes = [Glob('parameters*hpp'),'parse.h']
sources  = ['parse.tab.c', 'lex.yy.c', Glob('parameters*cpp') ]

#@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
# WHEN parse.y or parse.l change:
# 1. Uncomment below
# 2. scons -u type=serial
# 3. cp ../../build/serial/Parameters/lex.yy.c ../../build/serial/Parameters/parse.tab.? .
# 4. Recomment below
#@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

# env.CFile(target = 'lex.yy.c', source = 'parse.l')
# env.CFile(target = ['parse.tab.c', 'parse.tab.h'], source = 'parse.y', YACCFLAGS='-d -t')

#@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

#------------------------------
# ENVIRONMENTS
#------------------------------

env = env.Clone(LIBS=[components])

#------------------------------
# TARGETS
#------------------------------

libraries = env.Library ('parameters', [sources])

if (type == "charm"):
	sources = sources + ["../Main/main.cpp"]

test_parameters = env.Program (['test_Parameters.cpp', sources])
test_parse      = env.Program (['test_Parse.cpp',      sources])

binaries = [test_parameters, test_parse]

#------------------------------
# INSTALL
#------------------------------

env.Alias('install-bin',env.Install (bin_path,binaries))
env.Alias('install-inc',env.Install (inc_path,includes))
env.Alias('install-lib',env.Install (lib_path,libraries))



