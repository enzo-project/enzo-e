#------------------------------
# IMPORTS
#------------------------------

Import('env')
Import('type')

#------------------------------
# DEPENDENCIES
#------------------------------

# (uses Mesh for test_FieldBlock, which requires DataBlock)

if (type == "charm"):
    components = ['simulation','method',
                  'mesh',
                  'parallel','disk',
                  'parameters', 'monitor',
                  'performance','error',
                  'cello']
else:
    components = ['mesh',
                  'parallel','disk',
                  'monitor',
                  'performance','error',
                  'cello']

libraries  = ['png','hdf5']

#------------------------------
# ENVIRONMENTS
#------------------------------

env = env.Clone(LIBS=[components,libraries])
#------------------------------
# SOURCE
#------------------------------

includes = [Glob('field*hpp')]
sources  = [Glob('field*cpp')]

#------------------------------
# TARGETS
#------------------------------

libraries        = env.Library ('field', [sources])

if (type == "charm"):
	sources = sources + ["../main_simulation.cpp"]

test_field_block = env.Program (['test_FieldBlock.cpp',  sources])
test_field_descr = env.Program (['test_FieldDescr.cpp',  sources])
test_field_face  = env.Program (['test_FieldFace.cpp',  sources])

binaries = [test_field_block, test_field_descr, test_field_face]

#------------------------------
# INSTALL
#------------------------------

env.Alias('install-inc',env.Install ('#/include',includes))
env.Alias('install-lib',env.Install ('#/lib',    libraries))
env.Alias('install-bin',env.Install ('#/bin',    binaries))
env.Alias('install-bin',env.Install ('#/bin-'+type,binaries))




