
#------------------------------
# IMPORTS
#------------------------------

Import('env')
Import('parallel_type')


#------------------------------
# DIRECTORIES
#------------------------------

if (parallel_type == "charm"):
	SConscript('jacobi/SConscript')

#------------------------------
# DEPENDENCIES
#------------------------------

libraries  = ['error','png']

#------------------------------
# ENVIRONMENTS
#------------------------------

env = env.Clone(LIBS=[libraries])

#------------------------------
# SOURCE
#------------------------------

includes   = [Glob('parallel*hpp'),'parallel.def']
sources    = [Glob('parallel*cpp')]

if (parallel_type == "charm"):
	test = 'test_ParallelLayout'
	env.CharmBuilder(test + '.decl.h',test + '.ci',ARG = test)
	env.Depends(env.Object(test + '.cpp'),test + '.decl.h')

#------------------------------
# TARGETS
#------------------------------

libraries = env.Library ('parallel', [sources])
tests = []
if (parallel_type == "mpi" or parallel_type == "ampi"):
	test_GroupProcessMpi  =  env.Program (['test_GroupProcessMpi.cpp',    sources])
	tests.append(test_GroupProcessMpi)
	test_Mpi              = env.Program (['test_Mpi.cpp',      sources])
	tests.append(test_Mpi)

# test_ParallelAffinity = env.Program (['test_ParallelAffinity.cpp', sources])
test_ParallelLayout   = env.Program (['test_ParallelLayout.cpp',   sources])

tests.append(test_ParallelLayout)

#------------------------------
# INSTALL
#------------------------------

env.Alias('install-inc',env.Install ('#/include',includes))
env.Alias('install-lib',env.Install ('#/lib',    libraries))
env.Alias('install-bin',env.Install ('#/bin',    tests))


